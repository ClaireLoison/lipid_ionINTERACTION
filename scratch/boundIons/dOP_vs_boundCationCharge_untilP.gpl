set term pdfcairo enhanced rounded color dashed dashlength 1 size 10cm, 12.8cm
top = 0.99
heightY = 0.46
right = 0.975
left = 0.14
set out "dOP_vs_boundCationCharge_P.pdf"

unset key

w=1 # line width
ls_berger = 11
ls_beOPLS = 111
ls_chrm36 = 12
ls_lipd14 = 13
ls_macrog = 14
ls_orange = 15

r=0.7  # point radius
set pointsize r
pt_DPPC_Na = 4
pt_POPC_Na = 6
pt_POPC_Ca = 7

# Line- and point types for different force fields:
set style line ls_berger lt 1 dt 1 lw w lc rgb "#D70039"
set style line ls_beOPLS lt 1 dt 3 lw w lc rgb "#D70039"
set style line ls_chrm36 lt 1 dt 1 lw w lc rgb "#0088BF"
set style line ls_lipd14 lt 1 dt 1 lw w lc rgb "#FFD300"
set style line ls_macrog lt 1 dt 1 lw w lc rgb "#00A368"
set style line ls_orange lt 1 dt 1 lw w lc rgb "#FF6600"

set style fill transparent solid 0.25 noborder

set multiplot layout 2, 1 #tit " "
#set size square
set rmargin at screen right
set lmargin at screen left
set tmargin at screen top
set bmargin at screen top-heightY
unset xlabel
set xtics format '' 0.1
set mxtics 2
set ytics format "%3.2f" 0.02
set mytics 2
set ylabel "change of C-H order parameter from pure water case" offset 0.8,-9
plot [:][-0.11:] \
  "boundDensity_vs_dOP_untilP.orange"      u ($1):($4-$2+$3+$5):($4-$2-$3-$5) ev :::::1 w filledcurves ls ls_orange t "", \
  "boundDensity_vs_dOP_untilP.berger"      u ($1):($4-$2+$3+$5):($4-$2-$3-$5) ev :::::1 w filledcurves ls ls_berger t "", \
  "boundDensity_vs_dOP_untilP.berger-OPLS" u ($1):($4-$2+$3+$5):($4-$2-$3-$5) ev :::::1 w filledcurves ls ls_beOPLS t "", \
  "boundDensity_vs_dOP_untilP.macrog"      u ($1):($4-$2+$3+$5):($4-$2-$3-$5) ev :::::1 w filledcurves ls ls_macrog t "", \
  "boundDensity_vs_dOP_untilP.c36"         u ($1):($4-$2+$3+$5):($4-$2-$3-$5) ev :::::1 w filledcurves ls ls_chrm36 t "", \
  "boundDensity_vs_dOP_untilP.lipid14"     u ($1):($4-$2+$3+$5):($4-$2-$3-$5) ev :::::1 w filledcurves ls ls_lipd14 t "", \
    "boundDensity_vs_dOP_untilP.orange"      u ($1):($4-$2) ev :::::1 w l ls ls_orange t "", \
    "boundDensity_vs_dOP_untilP.berger"      u ($1):($4-$2) ev :::::1 w l ls ls_berger t "", \
    "boundDensity_vs_dOP_untilP.berger-OPLS" u ($1):($4-$2) ev :::::1 w l ls ls_beOPLS t "", \
    "boundDensity_vs_dOP_untilP.macrog"      u ($1):($4-$2) ev :::::1 w l ls ls_macrog t "", \
    "boundDensity_vs_dOP_untilP.c36"         u ($1):($4-$2) ev :::::1 w l ls ls_chrm36 t "", \
    "boundDensity_vs_dOP_untilP.lipid14"     u ($1):($4-$2) ev :::::1 w l ls ls_lipd14 t "", \
  "boundDensity_vs_dOP_untilP_typeseparation.orange" ev :::0::0 u ($1):($4-$2) w p ls ls_orange pt pt_POPC_Na t "", \
  "boundDensity_vs_dOP_untilP_typeseparation.orange" ev :::1::1 u ($1):($4-$2) w p ls ls_orange pt pt_POPC_Ca t "", \
  "boundDensity_vs_dOP_untilP_typeseparation.berger" ev :::0::0 u ($1):($4-$2) w p ls ls_berger pt pt_DPPC_Na t "", \
  "boundDensity_vs_dOP_untilP_typeseparation.berger" ev :::1::1 u ($1):($4-$2) w p ls ls_berger pt pt_POPC_Na t "", \
  "boundDensity_vs_dOP_untilP_typeseparation.berger" ev :::2::2 u ($1):($4-$2) w p ls ls_berger pt pt_POPC_Ca t "", \
  "boundDensity_vs_dOP_untilP.berger-OPLS" ev ::1:::1 u ($1):($4-$2) w p ls ls_beOPLS pt pt_DPPC_Na t "", \
  "boundDensity_vs_dOP_untilP.macrog" ev ::1:::1 u ($1):($4-$2) w p ls ls_macrog pt pt_POPC_Na t "", \
  "boundDensity_vs_dOP_untilP_typeseparation.c36" ev :::0::0 u ($1):($4-$2) w p ls ls_chrm36 pt pt_POPC_Na t "", \
  "boundDensity_vs_dOP_untilP_typeseparation.c36" ev :::1::1 u ($1):($4-$2) w p ls ls_chrm36 pt pt_POPC_Ca t "", \
  "boundDensity_vs_dOP_untilP_typeseparation.lipid14" ev :::0::0 u ($1):($4-$2) w p ls ls_lipd14 pt pt_POPC_Na t "", \
  "boundDensity_vs_dOP_untilP_typeseparation.lipid14" ev :::1::1 u ($1):($4-$2) w p ls ls_lipd14 pt pt_POPC_Ca t ""

set key at screen 0.45,0.49 opaque reverse Left bottom left box maxrows 4
set tmargin at screen top-heightY
set bmargin at screen top-2*heightY
set xlabel "bound cation charge (q/nm^{2}, integrated until P)" offset 0,0.5
set xtics format '%2.1f'
unset ylabel
plot [:][:]\
  "boundDensity_vs_dOP_untilP.orange"      u ($1):($4-$2+$3+$5):($4-$2-$3-$5) ev :::2 w filledcurves ls ls_orange t "", \
  "boundDensity_vs_dOP_untilP.berger"      u ($1):($4-$2+$3+$5):($4-$2-$3-$5) ev :::2 w filledcurves ls ls_berger t "", \
  "boundDensity_vs_dOP_untilP.berger-OPLS" u ($1):($4-$2+$3+$5):($4-$2-$3-$5) ev :::2 w filledcurves ls ls_beOPLS t "", \
  "boundDensity_vs_dOP_untilP.macrog"      u ($1):($4-$2+$3+$5):($4-$2-$3-$5) ev :::2 w filledcurves ls ls_macrog t "", \
  "boundDensity_vs_dOP_untilP.c36"         u ($1):($4-$2+$3+$5):($4-$2-$3-$5) ev :::2 w filledcurves ls ls_chrm36 t "", \
  "boundDensity_vs_dOP_untilP.lipid14"     u ($1):($4-$2+$3+$5):($4-$2-$3-$5) ev :::2 w filledcurves ls ls_lipd14 t "", \
    "boundDensity_vs_dOP_untilP.orange"      u ($1):($4-$2) ev :::2 w l ls ls_orange t "Orange", \
    "boundDensity_vs_dOP_untilP.berger"      u ($1):($4-$2) ev :::2 w l ls ls_berger t "Berger", \
    "boundDensity_vs_dOP_untilP.berger-OPLS" u ($1):($4-$2) ev :::2 w l ls ls_beOPLS t "Berger-OPLS", \
    "boundDensity_vs_dOP_untilP.macrog"      u ($1):($4-$2) ev :::2 w l ls ls_macrog t "MacRog", \
    "boundDensity_vs_dOP_untilP.c36"         u ($1):($4-$2) ev :::2 w l ls ls_chrm36 t "CHARMM36", \
    "boundDensity_vs_dOP_untilP.lipid14"     u ($1):($4-$2) ev :::2 w l ls ls_lipd14 t "Lipid14", \
   "boundDensity_vs_dOP_untilP_typeseparation.orange" ev :::2::2 u ($1):($4-$2) w p ls ls_orange pt pt_POPC_Na t "", \
   "boundDensity_vs_dOP_untilP_typeseparation.orange" ev :::3::3 u ($1):($4-$2) w p ls ls_orange pt pt_POPC_Ca t "", \
   "boundDensity_vs_dOP_untilP_typeseparation.berger" ev :::3::3 u ($1):($4-$2) w p ls ls_berger pt pt_DPPC_Na t "", \
   "boundDensity_vs_dOP_untilP_typeseparation.berger" ev :::4::4 u ($1):($4-$2) w p ls ls_berger pt pt_POPC_Na t "", \
   "boundDensity_vs_dOP_untilP_typeseparation.berger" ev :::5::5 u ($1):($4-$2) w p ls ls_berger pt pt_POPC_Ca t "", \
   "boundDensity_vs_dOP_untilP.berger-OPLS" ev ::1:2 u ($1):($4-$2) w p ls ls_beOPLS pt pt_DPPC_Na t "", \
   "boundDensity_vs_dOP_untilP.macrog" ev ::1:2 u ($1):($4-$2) w p ls ls_macrog pt pt_POPC_Na t "", \
   "boundDensity_vs_dOP_untilP_typeseparation.c36" ev :::2::2 u ($1):($4-$2) w p ls ls_chrm36 pt pt_POPC_Na t "", \
   "boundDensity_vs_dOP_untilP_typeseparation.c36" ev :::3::3 u ($1):($4-$2) w p ls ls_chrm36 pt pt_POPC_Ca t "", \
   "boundDensity_vs_dOP_untilP_typeseparation.lipid14" ev :::2::2 u ($1):($4-$2) w p ls ls_lipd14 pt pt_POPC_Na t "", \
   "boundDensity_vs_dOP_untilP_typeseparation.lipid14" ev :::3::3 u ($1):($4-$2) w p ls ls_lipd14 pt pt_POPC_Ca t ""
  
unset multiplot